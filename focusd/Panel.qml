import QtQuick
import Quickshell
import Quickshell.Io
import qs.Commons
import qs.Ui

Panel {
  id: root
  moduleName: "focusd"
  manageIpc: false

  property var anchorItem: null
  property var hostWidget: null
  property var timerService: null
  readonly property var barIdentity: hostWidget || root

  readonly property color foreground: Color.popups.text
  readonly property color activeColor: Color.accent
  readonly property string fontFamily: bar ? bar.fontFamily : Style.font.family
  readonly property string progressBarStyle: setting("progressBarStyle", "linear")
  property int selectedAction: 0
  property bool cursorActive: true

  property bool installed: false
  property bool checkingInstallation: true
  property bool installing: false
  property string installError: ""
  readonly property string installFailurePath: String(Quickshell.env("XDG_RUNTIME_DIR") || "") + "/focusd-panel-install.failed"

  property string currentPage: "timer"
  property bool checkingUpdate: false
  property bool updateAvailable: false
  property string latestVersion: ""

  function open() {
    currentPage = "timer";
    selectedAction = 0;
    cursorActive = true;
    controller.show();
    root.checkInstallation();
  }

  function close() {
    controller.hide();
  }

  function toggle() {
    if (opened)
      close();
    else
      open();
  }

  function actionCount() {
    if (!root.installed)
      return 1;
    if (currentPage === "info")
      return 3;
    return timerPage.actionCount();
  }

  function selectAction(delta) {
    cursorActive = true;
    if (!root.installed) {
      selectedAction = 0;
      return;
    }
    if (!timerService && currentPage === "timer") {
      selectedAction = 0;
      return;
    }
    var count = root.actionCount();
    selectedAction = ((selectedAction + delta) % count + count) % count;
  }

  function activateSelected() {
    if (!root.installed) {
      root.install();
      return;
    }
    if (!timerService)
      return;
    if (currentPage === "info") {
      if (selectedAction === 0)
        Qt.openUrlExternally("https://github.com/BibekBhusal0/focusd");
      else if (selectedAction === 1)
        Quickshell.execDetached(["focusd", "settings"]);
      else if (selectedAction === 2)
        Quickshell.execDetached(["focusd", "stats"]);
      return;
    }
    var idx = 0;
    if (selectedAction === idx && !timerService.stopped) {
      timerService.togglePause();
      return;
    }
    idx++;
    if (selectedAction === idx && !timerService.stopped) {
      timerService.skip();
      return;
    }
    idx++;
    if (timerPage.resetVisible && selectedAction === idx) {
      timerService.stop();
      return;
    }
    if (timerPage.resetVisible)
      idx++;
    if (timerPage.addTimeVisible && selectedAction === idx)
      timerService.addMinutes(5);
  }

  function actionHovered(index, hovered) {
    if (!hovered)
      return;
    cursorActive = true;
    selectedAction = index;
  }

  function switchPanel(direction) {
    if (bar && typeof bar.switchPanelFrom === "function")
      return bar.switchPanelFrom(barIdentity, direction);
    return false;
  }

  function checkInstallation() {
    if (whichProcess.running)
      return;
    root.checkingInstallation = true;
    whichProcess.command = ["sh", "-c", "if command -v focusd >/dev/null 2>&1; then exit 0; elif test -f \"$1\"; then cat \"$1\"; exit 2; else exit 1; fi", "sh", root.installFailurePath];
    whichProcess.running = true;
  }

  readonly property string focusdVersion: "v0.2.0"
  readonly property string focusdSha256: "13538979d894f5b8f665a0a392598a78e94078939fec478246329b37521a8595"
  readonly property string focusdDownloadUrl: "https://github.com/BibekBhusal0/focusd/releases/download/" + focusdVersion + "/focusd-linux-x86_64"

  function installCommand() {
    return "rm -f \"$XDG_RUNTIME_DIR/focusd-panel-install.failed\"; status=0; " + "mkdir -p \"$HOME/.local/bin\" && " + "tmp=$(mktemp) && " + "curl -fsSL \"" + focusdDownloadUrl + "\" -o \"$tmp\" && " + "actual=$(sha256sum \"$tmp\" | awk '{print $1}') && " + "if [ \"$actual\" = \"" + focusdSha256 + "\" ]; then " + "  mv \"$tmp\" \"$HOME/.local/bin/focusd\" && chmod +x \"$HOME/.local/bin/focusd\"; " + "else " + "  rm -f \"$tmp\"; status=1; " + "fi " + "|| status=$?; " + "if (( status != 0 )); then printf '%s\\n' \"$status\" > \"$XDG_RUNTIME_DIR/focusd-panel-install.failed\"; fi; " + "(exit \"$status\")";
  }

  function install() {
    root.installing = true;
    root.installError = "";
    installerProcess.command = ["sh", "-c", root.installCommand()];
    installerProcess.running = true;
    installPoll.restart();
    installTimeout.restart();
  }

  function checkForUpdates() {
    if (!root.installed || !whichProcess.running) {
      root.checkingUpdate = true;
      updateCheckProcess.command = ["sh", "-c", "command -v yay >/dev/null 2>&1 && yay -Qu focusd 2>/dev/null | grep -q '^focusd' && echo 'update' || echo 'current'"];
      updateCheckProcess.running = true;
    }
  }

  function updateFocusd() {
    if (root.installed) {
      var updateCmd = "if command -v yay >/dev/null 2>&1; then " + "alacritty -e sh -c 'yay -S focusd && echo \"\" && echo \"Update complete. Restarting daemon...\" && sleep 2 && focusd --stop-daemon && focusd -d && echo \"Done! Press Enter to close.\" && read'; " + "else " + "mkdir -p \"$HOME/.local/bin\" && " + "tmp=$(mktemp) && " + "curl -fsSL \"" + focusdDownloadUrl + "\" -o \"$tmp\" && " + "actual=$(sha256sum \"$tmp\" | awk '{print $1}') && " + "if [ \"$actual\" = \"" + focusdSha256 + "\" ]; then " + "  mv \"$tmp\" \"$HOME/.local/bin/focusd\" && chmod +x \"$HOME/.local/bin/focusd\" && " + "  focusd --stop-daemon && focusd -d; " + "else " + "  rm -f \"$tmp\"; " + "fi; " + "fi";
      Quickshell.execDetached(["sh", "-c", updateCmd]);
      root.close();
    }
  }

  Component.onCompleted: {
    root.checkInstallation();
    Qt.callLater(root.checkForUpdates);
  }

  KeyboardPanel {
    id: panel
    anchorItem: root.anchorItem
    owner: root.barIdentity
    bar: root.bar
    open: root.opened
    focusTarget: keyCatcher
    contentWidth: panel.fittedContentWidth(Style.space(390))
    contentHeight: panel.fittedContentHeight(content.implicitHeight)

    PanelKeyCatcher {
      id: keyCatcher
      anchors.fill: parent

      onMoveRequested: function (dx, dy) {
        if (dx !== 0)
          root.selectAction(dx);
        else if (dy !== 0)
          root.selectAction(dy);
      }
      onActivateRequested: root.activateSelected()
      onCloseRequested: {
        if (root.currentPage === "info") {
          root.currentPage = "timer";
          root.selectedAction = 0;
        } else {
          root.close();
        }
      }
      onTabRequested: function (direction) {
        root.switchPanel(direction);
      }

      Column {
        id: content
        width: parent.width
        spacing: Style.space(18)

        Column {
          visible: !root.installed && !root.checkingInstallation
          width: parent.width
          spacing: Style.space(16)

          Item {
            width: parent.width
            implicitHeight: Style.space(64)

            Text {
              anchors.centerIn: parent
              text: "󱎫"
              color: root.foreground
              font.family: root.fontFamily
              font.pixelSize: Style.font.display + Style.space(12)
            }
          }

          Column {
            width: parent.width
            spacing: Style.space(6)

            Text {
              width: parent.width
              text: "Focusd is not installed."
              color: root.foreground
              font.family: root.fontFamily
              font.pixelSize: Style.font.title
              font.bold: true
              horizontalAlignment: Text.AlignHCenter
              wrapMode: Text.WordWrap
            }

            Text {
              width: parent.width
              text: "Install the pomodoro daemon this widget controls."
              color: Qt.darker(root.foreground, 1.4)
              font.family: root.fontFamily
              font.pixelSize: Style.font.body
              horizontalAlignment: Text.AlignHCenter
              wrapMode: Text.WordWrap
            }
          }

          Button {
            width: parent.width
            text: root.installing ? "Installing focusd…" : "Install focusd"
            iconText: root.installing ? "" : "󰏔"
            iconSpinning: root.installing
            fontFamily: root.fontFamily
            fontSize: Style.font.body
            iconSize: Style.font.icon
            foreground: root.foreground
            accent: root.activeColor
            verticalPadding: Style.space(14)
            bordered: true
            selected: true
            hasCursor: root.cursorActive && root.selectedAction === 0
            enabled: !root.installing
            onHovered: function (hovered) {
              if (hovered) {
                root.cursorActive = true;
                root.selectedAction = 0;
              }
            }
            onClicked: root.install()
          }

          Text {
            visible: root.installError !== ""
            width: parent.width
            text: root.installError
            color: Color.urgent
            font.family: root.fontFamily
            font.pixelSize: Style.font.bodySmall
            wrapMode: Text.WordWrap
            horizontalAlignment: Text.AlignHCenter
          }
        }

        TimerPage {
          id: timerPage
          visible: root.installed && root.currentPage === "timer"
          timerService: root.timerService
          foreground: root.foreground
          activeColor: root.activeColor
          fontFamily: root.fontFamily
          progressBarStyle: root.progressBarStyle
          updateAvailable: root.updateAvailable
          cursorActive: root.cursorActive
          selectedAction: root.selectedAction
          onUpdateRequested: root.updateFocusd()
          onInfoRequested: {
            root.currentPage = "info";
            root.selectedAction = 0;
          }
          onActionHovered: function (index, hovered) {
            root.actionHovered(index, hovered);
          }
        }

        InfoPage {
          visible: root.installed && root.currentPage === "info"
          timerService: root.timerService
          foreground: root.foreground
          activeColor: root.activeColor
          fontFamily: root.fontFamily
          cursorActive: root.cursorActive
          selectedAction: root.selectedAction
          onActionHovered: function (index, hovered) {
            root.actionHovered(index, hovered);
          }
        }
      }
    }
  }

  Process {
    id: whichProcess
    stdout: StdioCollector {
      id: whichOutput
      waitForEnd: true
    }
    onExited: function (exitCode) {
      root.checkingInstallation = false;
      root.installed = exitCode === 0;
      if (root.installed) {
        root.installing = false;
        installPoll.stop();
        installTimeout.stop();
      } else if (exitCode === 2 && root.installing) {
        root.installing = false;
        installPoll.stop();
        installTimeout.stop();
        var exitStatus = String(whichOutput.text || "").trim();
        root.installError = exitStatus === "130" ? "Installation was canceled." : exitStatus === "1" ? "Checksum verification failed. The download may be corrupted." : "Installation did not finish. Check the Omarchy terminal and try again.";
      } else {
        root.installError = "";
      }
    }
  }

  Process {
    id: installerProcess
  }

  Process {
    id: updateCheckProcess
    stdout: StdioCollector {
      id: updateCheckOutput
      waitForEnd: true
    }
    onExited: function (exitCode) {
      root.checkingUpdate = false;
      if (exitCode === 0) {
        var output = String(updateCheckOutput.text || "").trim();
        root.updateAvailable = output === "update";
      }
    }
  }

  Timer {
    id: installPoll
    interval: 1000
    repeat: true
    running: root.installing && !root.installed
    onTriggered: root.checkInstallation()
  }

  Timer {
    id: installTimeout
    interval: 300000
    onTriggered: {
      if (!root.installing)
        return;
      root.installing = false;
      installPoll.stop();
      root.installError = "Installation is still waiting. Check the Omarchy terminal and try again.";
    }
  }
}
